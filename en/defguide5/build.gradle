buildscript {
  repositories {
    mavenCentral()
    maven { url "http://maven.restlet.org" }
  }

  dependencies {
    classpath group: 'org.docbook', name: 'docbook-xslt2', version: docbookXsltVersion
    classpath group: 'com.xmlcalabash', name: 'xmlcalabash1-print', version: '1.1.4'
    classpath group: 'com.xmlcalabash', name: 'xmlcalabash1-gradle', version: '1.1.2'
  }
}

plugins {
  id "de.undercouch.download" version "2.0.0"
}

repositories {
  mavenLocal()
  mavenCentral()
}

apply plugin: 'org.docbook.task'
apply plugin: 'com.xmlcalabash.task'

import org.docbook.DocBookTask
import com.xmlcalabash.XMLCalabashTask
import de.undercouch.gradle.tasks.download.Download

defaultTasks 'tdg'

project.ext.docbookXslt = "docbook-xslt2-$docbookXsltVersion"

task downloadDocBook(type: Download) {
  src docbookXsltBaseUri + '/release/' + docbookXsltVersion + '/' + docbookXslt + '.zip'
  dest new File(buildDir, docbookXslt + '.zip')
}
downloadDocBook.onlyIf { !file("$buildDir/${docbookXslt}.zip").exists() }

task setupDocBook(dependsOn: downloadDocBook, type: Copy) {
  from zipTree(downloadDocBook.dest)
  into { "build" }
  doLast {
    copy {
      from "build/$docbookXslt"
      into 'build/docbook'
    }
  }
}
setupDocBook.onlyIf { !file("$buildDir/docbook").exists() }

task gitLogSummary(type: Exec) {
  commandLine "../bin/git-log-summary"
  standardOutput = new FileOutputStream(new File("lib/gitlog.xml"))
}

task refpageElements(dependsOn: ['gitLogSummary'], type: XMLCalabashTask) {
  inputs.dir "../refpages/elements"
  inputs.dir "lib"
  inputs.file "../stylesheets/refentry.xsl"
  inputs.file "../xpl/refpages.xpl"
  outputs.dir "build/elements"

  input("source", "lib/$rnd")
  pipeline "../xpl/refpages.xpl"
}

task refpageListElements(type: XMLCalabashTask) {
  inputs.file "lib/$rnd"
  inputs.file "../stylesheets/elemlist.xsl"
  inputs.file "../xpl/element-list.xpl"
  outputs.file "build/list-elements.xml"

  input("source", "lib/$rnd")
  output("result", "build/list-elements.xml")
  pipeline "../xpl/element-list.xpl"

  doFirst {
    mkdir("build")
  }
}

task refpageListAssembly(type: XMLCalabashTask) {
  inputs.file "lib/$rnd"
  inputs.file "../stylesheets/elemlist.xsl"
  inputs.file "../xpl/element-list.xpl"
  outputs.file "build/list-assembly.xml"

  input("source", "lib/$rnd")
  output("result", "build/list-assembly.xml")
  pipeline "../xpl/element-list.xpl"
  option("manual","assembly")

  doFirst {
    mkdir("build")
  }
}

task copyRefpages(type: Copy) {
  FileCollection refpages = fileTree(dir: 'refpages')
  from refpages
  into "build/"

  doFirst {
    mkdir("build")
  }
}

task refpages(dependsOn: ['setupDocBook', 'refpageElements', 'copyRefpages',
                          'refpageListElements', 'refpageListAssembly']) {
  // nothing to see here
}

task patterns(type: XMLCalabashTask) {
  inputs.file "lib/patterns.xml"
  inputs.file "../stylesheets/patterns.xsl"
  inputs.file "lib/$rnd"
  inputs.file "../xpl/style.xpl"
  outputs.file "build/patterns.xml"

  input("source", "lib/patterns.xml")
  input("stylesheet", "../stylesheets/patterns.xsl")
  output("result", "build/patterns.xml")
  param("rngfile", "../defguide5/lib/$rnd")
  pipeline "../xpl/style.xpl"

  doFirst {
    mkdir("build")
  }
}

task bookmeta(type: XMLCalabashTask) {
  inputs.file "src/bookinfo.xml"
  inputs.file "gradle.properties"
  outputs.file "build/bookinfo.xml"
  input("source", "src/bookinfo.xml")
  output("result", "build/bookinfo.xml")
  param("bookVersion", bookVersion)
  param("docbookVersion", docbookVersion)
  pipeline "../xpl/bookmeta.xpl"

  doFirst {
    mkdir("build")
  }
}

task tdgxml(dependsOn: ['bookmeta', 'patterns', 'refpages'], type: XMLCalabashTask) {
  inputs.dir "refpages"
  inputs.dir "src"
  inputs.file "../xpl/xinclude.xpl"
  outputs.file "build/tdg.xml"

  input("source", "src/book5.xml")
  output("result", "build/tdg.xml")
  pipeline "../xpl/xinclude.xpl"
}

task book(dependsOn: ['tdgxml', 'setupDocBook'], type: XMLCalabashTask) {
  inputs.file file("build/tdg.xml")
  inputs.file file("../xpl/make-book.xpl")
  inputs.file file("build/docbook/xslt/base/preprocess/30-profile.xsl")
  inputs.file file("../stylesheets/purpose.xsl")
  inputs.file file("../stylesheets/tdg2db.xsl")
  inputs.file file("../stylesheets/profile.xsl")
  outputs.file file("build/book.xml")

  input("source", "build/tdg.xml")
  output("result", "build/book.xml")
  option("arch", "assembly defguide5")
  option("revision", "5.0 5.1")
  // This crazy list of conditions is a side effect of the fact that
  // I carelessly decided to override the condition attribute for
  // something else inthe reference pages. :-(
  option("condition", "expanded ref.desc.attribute-descriptions ref.desc.attributes ref.desc.children ref.desc.parents ref.description ref.desc.rules ref.desc.seealso ref.examples refpages web")
  pipeline "../xpl/make-book.xpl"
}

task copyCss(type: Copy) {
  from "../resources/css"
  into "build/html/css"
}

task copyJs(type: Copy) {
  from "../resources/js"
  into "build/html/js"
}

task copyHtaccess(type: Copy) {
  from "../resources/etc/"
  into "build/html/"
}

task copySourceFigs(type: Copy) {
  from "../resources/figs/"
  from "figs/"
  into "build/figs/"
}

task copyWebFigs(type: Copy) {
  from "../resources/figs/web"
  from "figs/web"
  into "build/html/figs/web/"
}

task copyFigs(dependsOn: ['copySourceFigs', 'copyWebFigs']) {
  // nothing to see here
}

task copyFiles(dependsOn: ['copyCss', 'copyJs', 'copyHtaccess', 'copyFigs']) {
  // nothing to see here
}

task tdg(dependsOn: ['book', 'copyFiles'], type: DocBookTask) {
  inputs.file "build/book.xml"
  outputs.dir "build/html"
  inputs.file "../stylesheets/chunk.xsl"

  input "build/book.xml"
  style "../stylesheets/chunk.xsl"
  param("output.dir", System.getProperty('user.dir') + "/build/")
  param("base.dir", "build/html/")
  param("html.ext", ".html")
  param("rngfile", "lib/$rnd")
  param("resource.root", "")
}

task tdg_fake(dependsOn: ['copyFiles']) {
  // nothing to see here
}

task test(dependsOn: ['tdg']) {
  // nothing to see here
}

task clean
task clean.doFirst {
  delete "build"
}
