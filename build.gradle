buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://maven.restlet.talend.com" }
  }

  // Get rid of that [expletive deleted] warning about xml-apis 2.0.2/1.0.b2
  configurations.all {
    resolutionStrategy {
      force 'xml-apis:xml-apis:1.4.01'
      force "com.xmlcalabash:xmlcalabash:${xmlCalabashVersion}"
    }
  }

  dependencies {
    classpath group: 'org.docbook', name: 'docbook-xslt2', version: '2.5.0'
    classpath group: 'org.docbook', name: 'docbook-schemas', version: '5.1-1'
    classpath group: 'com.xmlcalabash', name: 'xmlcalabash1-print', version: '1.1.5'
    classpath group: 'com.xmlcalabash', name: 'xmlcalabash1-gradle', version: '1.4.0'
    classpath group: 'com.xmlcalabash', name: 'xmlcalabash', version: xmlCalabashVersion
    classpath group: 'org.xmlresolver', name: 'xmlresolver', version: xmlresolverVersion
    classpath group: 'org.docbook', name: 'docbook-xslTNG', version: docbookXsltVersion
  }
}

plugins {
  id "groovy"
  id "de.undercouch.download" version "4.0.4"
  id 'com.nwalsh.gradle.saxon.saxon-gradle' version '0.9.3'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://dev.saxonica.com/maven" }
  maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

// Get rid of that [expletive deleted] warning about xml-apis 2.0.2/1.0.b2
configurations.all {
  resolutionStrategy {
    force 'xml-apis:xml-apis:1.4.01'
    force "com.xmlcalabash:xmlcalabash:${xmlCalabashVersion}"
    force "org.xmlresolver:xmlresolver:${xmlresolverVersion}"
  }
}

configurations {
  saxonee.extendsFrom(implementation)
  tools {
    description = "Run tools"
    transitive = true
  }
}

dependencies {
  implementation (
    [group: 'com.saxonica', name: 'Saxon-EE', version: saxonVersion],
    [group: 'org.relaxng', name: 'jing', version: '20181204'],
    [group: 'org.docbook', name: 'docbook-xslTNG', version: docbookXsltVersion],
    [group: 'org.xmlresolver', name: 'xmlresolver', version: xmlresolverVersion]
  )
  tools (
    [group: 'org.relaxng', name: 'jing', version: '20181204'],
    [group: 'org.relaxng', name: 'trang', version: '20181204']
  )
}

defaultTasks "dist"

apply plugin: 'org.docbook.task'
apply plugin: 'com.xmlcalabash.task'

import org.docbook.DocBookTask
import com.xmlcalabash.XMLCalabashTask
import com.nwalsh.gradle.saxon.SaxonXsltTask
import de.undercouch.gradle.tasks.download.Download

// ======================================================================

def absuri(String path) {
  def spath = path
  if (!spath.endsWith("/")) {
    spath = spath + "/"
  }
  def cpath = new File(spath).getCanonicalPath()
  return "file://" + cpath + "/"
}

// ======================================================================
// Define some globals

def books = ["defguide",
             "publishers",
             "sdocbook",
             "slides",
             "website"]

def bookVersion = ["defguide": defguideBookVersion,
                   "publishers": publishersBookVersion,
                   "sdocbook": sdocbookBookVersion,
                   "slides": slidesBookVersion,
                   "website": websiteBookVersion ]

def profileCondition = "expanded ref.desc.attribute-descriptions ref.desc.attributes ref.desc.children ref.desc.parents ref.description ref.desc.rules ref.desc.seealso ref.examples refpages web"

def bookCondition = ["defguide": profileCondition,
                     "publishers": profileCondition,
                     "sdocbook": profileCondition,
                     "slides": profileCondition,
                     "website": profileCondition]

def bookArch = ["defguide": "assembly defguide",
                "publishers": "publishers",
                "sdocbook": "sdocbook",
                "slides": "sdocbook slides",
                "website": "slides website"]

def bookRevision = ["defguide": "5.0 5.1 5.2",
                    "publishers": "5.0 5.1 5.2 1.0 1.1 1.2",
                    "sdocbook": "5.0 5.1 5.2",
                    "slides": "5.0 5.1 5.2",
                    "website": "5.0 5.1 5.2"]

// ======================================================================

task helloWorld() {
  doFirst {
    println("Hello, world.")
    configurations.saxonee.resolve().each { path ->
      println(path)
    }
  }
}

// ======================================================================

task gitLogSummary(type: Exec) {
  inputs.file "tools/bin/git-log-summary"
  outputs.file "${buildDir}/gitlog.xml"
  commandLine "tools/bin/git-log-summary", "-o", "${buildDir}/gitlog.xml"
  doFirst {
    mkdir("build")
  }
}

task allBooks() {
  // just a dependency
}

task dist() {
  // just a dependency
}

clean {
  doFirst {
    delete("build")
  }
}

// ======================================================================
// Generate tasks parameterized by vocabulary

books.each { vocab ->
  def rndFile = new File("${projectDir}/src/lib/${vocab}.rnd").getCanonicalPath()

  task "${vocab}_patterns"(type: XMLCalabashTask) {
    input("source", "${projectDir}/src/${vocab}/patterns.xml")
    input("stylesheet", "${projectDir}/tools/xsl/patterns.xsl")
    output("result", "${buildDir}/${vocab}/patterns.xml")
    param("rngfile", rndFile)
    pipeline "tools/xpl/style.xpl"
    doFirst {
      mkdir("${buildDir}/${vocab}")
    }
  }

  task "${vocab}_refpageExamples"(type: Copy) {
    from "${projectDir}/src/refpages/examples"
    into "${buildDir}/${vocab}/refpages/examples"
    include "*"
    doFirst {
      mkdir("${buildDir}/${vocab}/refpages/examples")
    }
  }

  task "${vocab}_bookmeta"(type: XMLCalabashTask) {
    input("source", "${projectDir}/src/${vocab}/bookinfo.xml")
    output("result", "${buildDir}/${vocab}/bookinfo.xml")
    param("bookVersion", bookVersion[vocab])
    param("docbookVersion", docbookVersion)
    pipeline "tools/xpl/bookmeta.xpl"
    doFirst {
      mkdir("${buildDir}/${vocab}")
    }
  }

  task "${vocab}_refpageListElements"(type: XMLCalabashTask) {
    inputs.file "tools/xsl/elem-xi.xsl"
    inputs.file "tools/xpl/element-list.xpl"
    input("source", rndFile)
    output("result", "${buildDir}/${vocab}/list-elements.xml")
    pipeline "tools/xpl/element-list.xpl"
    doFirst {
      mkdir("${buildDir}/${vocab}")
    }
  }

  task "${vocab}_copysrc"(type: Copy) {
    from "${projectDir}/src/${vocab}"
    into "${buildDir}/${vocab}"
    include "**/*"
    exclude "bookinfo.xml"
    exclude "patterns.xml"
    exclude "list-elements.xml"
    exclude "list-assembly.xml" // only in defguide, but harmless
  }

  task "${vocab}_copyresources"(dependsOn: ["${vocab}_copyresources_src",
                                            "${vocab}_copyresources_docbook"]) {
    // Once so the stylesheet can read the dimensions
    // Once so it's in the actual published output
  }

  task "${vocab}_copyresources_src"(type: Copy,
                                    dependsOn: ["${vocab}_copyresources_src_html"]) {
    from "${projectDir}/src/resources"
    into "${buildDir}/${vocab}"
    include "**/*"
    exclude "figs/src"
    exclude "figs/print"
  }

  task "${vocab}_copyresources_src_html"(type: Copy) {
    from "${projectDir}/src/resources"
    into "${buildDir}/${vocab}/html"
    include "**/*"
    exclude "figs/src"
    exclude "figs/print"
    doFirst {
      mkdir "${buildDir}/${vocab}/html"
    }
  }

  task "${vocab}_copyresources_docbook"(dependsOn: ["${vocab}_copyresources_src_html"]) {
    def dbjar = null
    configurations.saxonee.each { path ->
      if (path.toString().contains("docbook-xslTNG")) {
        dbjar = path
      }
    }

    doLast {
      if (dbjar == null) {
        throw new GradleException("Failed to locate DocBook xslTNG jar file")
      }
      copy {
        into "${buildDir}/${vocab}/html"
        from ({ zipTree(dbjar.toString()) }) {
          include "org/docbook/xsltng/resources/**"
        }
        eachFile { fileCopyDetails ->
          def originalPath = fileCopyDetails.path
          fileCopyDetails.path = originalPath.replace('org/docbook/xsltng/resources/', '')
        }
      }
    }

    doLast {
      delete "${buildDir}/${vocab}/html/org"
    }
  }

  task "${vocab}_tdgxml"(dependsOn: ["${vocab}_copysrc",
                                     "${vocab}_bookmeta",
                                     "${vocab}_patterns",
                                     "${vocab}_refpages",
                                     "${vocab}_copyresources_src",
                                     "${vocab}_copyresources_src_html",
                                     "${vocab}_copyresources_docbook"],
                         type: XMLCalabashTask) {
    inputs.dir "${buildDir}/${vocab}"
    input("source", "${buildDir}/${vocab}/book5.xml")
    output("result", "${buildDir}/${vocab}/tdg.xml")
    pipeline "tools/xpl/xinclude.xpl"
  }

  task "${vocab}_refpages"(dependsOn: ["${vocab}_refpageListElements",
                                       "${vocab}_refpageExamples",
                                       "gitLogSummary"],
                           type: XMLCalabashTask) {
    inputs.dir "${projectDir}/src/refpages/elements"
    inputs.file "tools/xsl/refentry.xsl"
    inputs.file "tools/xpl/refpages.xpl"
    inputs.file "${projectDir}/src/${vocab}/seealso.xml"
    outputs.dir "${buildDir}/${vocab}/refpages"
    option("rnd", rndFile)
    option("src", absuri("${projectDir}/src/${vocab}/"))
    option("dst", absuri("${buildDir}/${vocab}/refpages/"))
    outputs.dir "${buildDir}/${vocab}/refpages"
    input("source", rndFile)
    pipeline "tools/xpl/refpages.xpl"
  }

  task "${vocab}"(type: SaxonXsltTask,
                  dependsOn: ["${vocab}_xml",
                              "${vocab}_copyresources"]) {
    input "${buildDir}/${vocab}/book.xml"
    stylesheet "tools/xsl/book.xsl"
    output "${buildDir}/${vocab}/html/index.html"
    initializer "org.docbook.xsltng.extensions.Register"
    entityResolverClass "org.xmlresolver.Resolver"
    sourceParserClass "org.xmlresolver.tools.ResolvingXMLReader"
    styleParserClass "org.xmlresolver.tools.ResolvingXMLReader"
    parameters (
      'rngfile': rndFile,
      'docbookXsltVersion': docbookXsltVersion,
      'bookVersion': bookVersion[vocab],
      'mediaobject-input-base-uri': "file:${projectDir}/build/${vocab}/",
      'mediaobject-output-base-uri': "./",
      'chunk': "index.html",
      'chunk-output-base-uri': "${buildDir}/${vocab}/html/",
      'profile-condition': bookCondition[vocab],
      'profile-arch': bookArch[vocab],
      'persistent-toc': true,
      'profile-revision': bookRevision[vocab]
    )
    doFirst {
      mkdir("${buildDir}/${vocab}/html")
    }
  }
  allBooks.dependsOn "${vocab}"

  task "${vocab}_dist"(type: Copy,
                       dependsOn: ["${vocab}"]) {
    def distTarget = "${buildDir}/dist/${vocab}/${docbookBaseVersion}"
    if (vocab == "defguide") {
      distTarget = "${buildDir}/dist/${docbookBaseVersion}"
    }
    into distTarget
    from "${buildDir}/${vocab}/html"
  }
  dist.dependsOn "${vocab}_dist"
}

// ======================================================================

defguide_refpages.dependsOn "defguide_refpageListAssembly"

task defguide_refpageListAssembly(type: XMLCalabashTask) {
  inputs.file "tools/xsl/elem-xi.xsl"
  inputs.file "tools/xpl/element-list.xpl"

  input("source", new File(defguideRnd).getCanonicalPath())
  output("result", "${buildDir}/defguide/list-assembly.xml")
  pipeline "tools/xpl/element-list.xpl"

  option("manual","assembly")

  doFirst {
    mkdir("build")
  }
}

task defguide_xml(dependsOn: ['defguide_tdgxml'], type: XMLCalabashTask) {
  inputs.file file("tools/xsl/purpose.xsl")
  inputs.file file("tools/xsl/tdg2db.xsl")
  inputs.file file("${buildDir}/defguide/style/purpose.xsl")

  input("source", "${buildDir}/defguide/tdg.xml")
  output("result", "${buildDir}/defguide/book.xml")
  option("arch", "assembly defguide")
  option("revision", "5.0 5.1 5.2")
  // This crazy list of conditions is a side effect of the fact that
  // I carelessly decided to override the condition attribute for
  // something else in the reference pages. :-(
  option("condition", "expanded ref.desc.attribute-descriptions ref.desc.attributes ref.desc.children ref.desc.parents ref.description ref.desc.rules ref.desc.seealso ref.examples refpages web")
  pipeline "tools/xpl/make-book.xpl"
}

// ======================================================================

task publishers_xml(dependsOn: ['publishers_tdgxml'], type: XMLCalabashTask) {
  inputs.file file("tools/xsl/purpose.xsl")
  inputs.file file("tools/xsl/tdg2db.xsl")
  inputs.file file("tools/xsl/profile.xsl")
  inputs.file file("${buildDir}/publishers/style/purpose.xsl")

  input("source", "${buildDir}/publishers/tdg.xml")
  output("result", "${buildDir}/publishers/book.xml")
  option("arch", "publishers")
  option("revision", "5.0 5.1 5.2 1.0 1.1 1.2")
  // This crazy list of conditions is a side effect of the fact that
  // I carelessly decided to override the condition attribute for
  // something else in the reference pages. :-(
  option("condition", "expanded ref.desc.attribute-descriptions ref.desc.attributes ref.desc.children ref.desc.parents ref.description ref.desc.rules ref.desc.seealso ref.examples refpages web")
  pipeline "tools/xpl/make-book.xpl"
}

// ======================================================================

task sdocbook_xml(dependsOn: ['sdocbook_tdgxml'], type: XMLCalabashTask) {
  inputs.file file("tools/xsl/purpose.xsl")
  inputs.file file("tools/xsl/tdg2db.xsl")
  inputs.file file("tools/xsl/profile.xsl")
  inputs.file file("${buildDir}/sdocbook/style/purpose.xsl")

  input("source", "${buildDir}/sdocbook/tdg.xml")
  output("result", "${buildDir}/sdocbook/book.xml")
  option("arch", "sdocbook")
  option("revision", "5.0 5.1 5.2")
  // This crazy list of conditions is a side effect of the fact that
  // I carelessly decided to override the condition attribute for
  // something else in the reference pages. :-(
  option("condition", "expanded ref.desc.attribute-descriptions ref.desc.attributes ref.desc.children ref.desc.parents ref.description ref.desc.rules ref.desc.seealso ref.examples refpages web")
  pipeline "tools/xpl/make-book.xpl"
}

// ======================================================================

task slides_xml(dependsOn: ['slides_tdgxml'], type: XMLCalabashTask) {
  inputs.file file("tools/xsl/purpose.xsl")
  inputs.file file("tools/xsl/tdg2db.xsl")
  inputs.file file("tools/xsl/profile.xsl")
  inputs.file file("${buildDir}/slides/style/purpose.xsl")

  input("source", "${buildDir}/slides/tdg.xml")
  output("result", "${buildDir}/slides/book.xml")
  option("arch", "sdocbook slides")
  option("revision", "5.0 5.1 5.2")
  // This crazy list of conditions is a side effect of the fact that
  // I carelessly decided to override the condition attribute for
  // something else in the reference pages. :-(
  option("condition", "expanded ref.desc.attribute-descriptions ref.desc.attributes ref.desc.children ref.desc.parents ref.description ref.desc.rules ref.desc.seealso ref.examples refpages web")
  pipeline "tools/xpl/make-book.xpl"
}

// ======================================================================

task website_xml(dependsOn: ['website_tdgxml'], type: XMLCalabashTask) {
  inputs.file file("tools/xsl/purpose.xsl")
  inputs.file file("tools/xsl/tdg2db.xsl")
  inputs.file file("tools/xsl/profile.xsl")
  inputs.file file("${buildDir}/website/style/purpose.xsl")

  input("source", "${buildDir}/website/tdg.xml")
  output("result", "${buildDir}/website/book.xml")
  option("arch", "slides website")
  option("revision", "5.0 5.1 5.2")
  // This crazy list of conditions is a side effect of the fact that
  // I carelessly decided to override the condition attribute for
  // something else in the reference pages. :-(
  option("condition", "expanded ref.desc.attribute-descriptions ref.desc.attributes ref.desc.children ref.desc.parents ref.description ref.desc.rules ref.desc.seealso ref.examples refpages web")
  pipeline "tools/xpl/make-book.xpl"
}
